plugins {
  alias libs.plugins.spring.boot
  alias libs.plugins.spring.dependencies
  id 'no.ntnu.iir.idata2304.iot.java-application-conventions'
  alias libs.plugins.graalvm.native
}

dependencies {
  implementation libs.spring.starter
  implementation libs.spring.starter.web
  implementation libs.spring.starter.jpa
  implementation libs.spring.starter.actuator
  
  implementation libs.bundles.mqtt

  implementation libs.liquibase 
  implementation libs.sql.h2
  implementation libs.sql.postgres

  compileOnly libs.lombok

  developmentOnly libs.spring.devtools

  annotationProcessor libs.lombok
  annotationProcessor libs.spring.config.processor

  testImplementation testLibs.spring.starter.test
}

task copyFrontend(type: Copy) {
 from "$rootDir/apps/frontend/dist"
 into "$rootDir/apps/ingest/build/resources/main/static/."
}

copyFrontend.dependsOn(":apps:frontend:buildProduction")
bootJar.dependsOn(copyFrontend)
nativeCompile.dependsOn(copyFrontend)

tasks.named('test') {
  useJUnitPlatform()
}

graalvmNative {
  binaries {
    main {
      buildArgs.add('-H:IncludeResourceBundles=org.eclipse.paho.client.mqttv3.internal.nls.logcat,org.eclipse.paho.client.mqttv3.internal.nls.messages')
      buildArgs.add('-H:ReflectionConfigurationResources=reflection-config.json')
      buildArgs.add('-H:ResourceConfigurationResources=resource-config.json')
      buildArgs.add('-H:+ReportExceptionStackTraces')
    }
  }
}
